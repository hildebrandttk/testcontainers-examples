buildscript {
   repositories {
      mavenCentral()
   }
   dependencies {
      classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
   }
}

plugins {
   id "com.github.ben-manes.versions" version "0.33.0"
}

group 'tk.hildebrandt.testcontainers'

subprojects {
   apply plugin: 'java'

   sourceCompatibility = '1.8'
   targetCompatibility = '1.8'

   repositories {
      mavenCentral()
   }
   configurations.all {
      resolutionStrategy {
         failOnVersionConflict()

         force "org.springframework.data:spring-data-commons:${springBootVersion}",
            "org.springframework.data:spring-data-relational:${springDataJdbcVersion}",
            "org.slf4j:slf4j-api:${slf4jVersion}",
            "junit:junit:${junit4Version}",
            "org.junit.jupiter:junit-jupiter-api:${junit5Version}",
            "org.junit.jupiter:junit-jupiter:${junit5Version}",
            "org.junit.vintage:junit-vintage-engine:${junit5Version}",
            "org.apiguardian:apiguardian-api:1.1.2",
            "io.cucumber:cucumber-core:${cucumberVersion}",
            "org.apache.ant:ant:${antVersion}",
            "org.apache.ant:ant-launcher:${antVersion}",
            "io.cucumber:messages:13.0.1",
            "org.apache.commons:commons-lang3:3.12.0",
            "commons-logging:commons-logging:1.2",
            "net.java.dev.jna:jna:5.5.0",
            "com.google.guava:guava:30.1.1-jre",
            "org.testng:testng:${testNgVersion}",
            "org.checkerframework:checker-qual:3.5.0"

         eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.codehaus.groovy') {
               details.useVersion groovyVersion
            }
            if (details.requested.group == 'org.testcontainers') {
               details.useVersion testContainersVersion
            }
            if (details.requested.group.startsWith('com.fasterxml.jackson')) {
               details.useVersion '2.12.2'
            }
            if (details.requested.group == 'org.springframework') {
               details.useVersion springVersion
            }
            if (details.requested.group == 'org.springframework.boot') {
               details.useVersion springBootVersion
            }
         }
      }
   }
}
